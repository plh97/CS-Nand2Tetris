// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    // FIRST MUX
    // Create Not because Mux leads to A if instruction[15] is 0
    // Selector for first Mux
    Not(in=instruction[15], out=NOTinstr15);
    Mux16(a= outALU, b= instruction, sel=NOTinstr15, out=outMux1);

    // A
    // Will go to A register if Instr[15] is 0 OR Instr[5] is 1 (address of M)
    Or(a=NOTinstr15, b=instruction[5], out=Aload);
    ARegister(in=outMux1, load=Aload,out= Aout, out[0..14] = addressM);

    // SECOND MUX
    // To get AM, use M and then get A from A Instr or Instr C (instruction[15] = 1) 
    And(a= instruction[15], b=instruction[12], out= AMsel);
    Mux16(a=Aout, b=inM, sel= AMsel, out= AM);

    // ALU
     ALU(x=outD, y=AM, zx= instruction[11], nx=instruction[10], zy= instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6] ,out=outM, out=outALU, zr=zr,ng=ng); 

    // D
    // Will go to D register if Instr[15] is 1 and Instr[4] is 1
    And(a=instruction[15], b=instruction[4], out=Dload);
    DRegister(in=outALU, load=Dload, out=outD);

    // ZR
    // if zr, instr[1] and instr[15] are all true, then out is 0
    And(a=instruction[1], b=instruction[15], out=zrout);
    And(a=zrout, b=zr, out=outzr);

    // NG
    // if ng, instr[2] and instr[15] are true, out < 0 (negative)
    And(a=instruction[2], b=instruction[15], out=ngout);
    And(a=ng, b=ngout, out=outng);

    // !ZR AND !NG
    // therefore positive
    Not(in=zr, out=NOTzr);
    Not(in=ng, out=NOTng);
    // instr[15] means this is C-instruction, and instr[0] means > 0
    And(a=instruction[0], b=instruction[15], out=outAND);
    And(a=NOTzr, b=NOTng, out=NOTzrng);
    And(a=outAND, b=NOTzrng, out=outgreater0);


    // WRITEM
    // write if instr[15] and instr[3] are both 1 (computation and also d3)
    And(a=instruction[15], b=instruction[3], out=writeM);

    // CONTROL BITS FOR LOADING PC
    // determines if jumping/not jumping
    Or(a=outng, b=outzr, out=loadzrng);
    Or(a=loadzrng, b=outgreater0, out=loadjump);

    // PC
    // if we jump, PC = A; if no jump, PC increments; reset if reset is true
    Not(in=loadjump, out=NOTloadjump);
    PC(in=Aout,load=loadjump,inc=NOTloadjump,reset=reset,out[0..14]=pc); 
}