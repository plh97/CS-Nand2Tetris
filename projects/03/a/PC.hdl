// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

		// reset -> 0 
		// load  -> in[t]
		// inc1  -> out[t]+1
		// read  -> out[t]
		Inc16(in=oo,out=incd);

		Mux16(a=oo, b=incd, sel=inc, out=out1);
		Mux16(a=out1, b=in, sel=load, out=out2);
		Mux16(a=out2, b[0..15]=false, sel=reset, out=out3);
		
		Register(in=out3,load=true,out=out,out=oo);
}
